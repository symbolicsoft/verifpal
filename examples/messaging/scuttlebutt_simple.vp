// SPDX-FileCopyrightText: Â© 2019-2021 Nadim Kobeissi <nadim@symbolic.software>
// SPDX-License-Identifier: GPL-3.0-only

attacker[active]

// Setup

principal Alice[
	knows private longTermA
	generates ephemeralA
	longTermAPub = G^longTermA
	ephemeralAPub = G^ephemeralA
]

principal Bob[
	knows private longTermB
	generates ephemeralB
	longTermBPub = G^longTermB
	ephemeralBPub = G^ephemeralB
]

Bob -> Alice: longTermBPub

// 1. Client Hello

Alice -> Bob: ephemeralAPub

// 2. Server Hello

Bob -> Alice: ephemeralBPub

// 3. Client Authenticate

principal Alice[
	ephemeralSecretAlice = ephemeralBPub^ephemeralA
	longTermSecretAlice = longTermBPub^ephemeralA
	masterSecret1Alice = HASH(ephemeralSecretAlice, longTermSecretAlice)
	sig1Alice = SIGN(longTermA, HASH(longTermBPub, ephemeralSecretAlice))
	secretBox1Alice = AEAD_ENC(masterSecret1Alice, sig1Alice, nil)
	secretBox2Alice = AEAD_ENC(masterSecret1Alice, longTermAPub, nil)
	longEphemeralSecretAlice = ephemeralBPub^longTermA
	masterSecret2Alice = HASH(ephemeralSecretAlice, longTermSecretAlice, longEphemeralSecretAlice)
]

// TODO: Concatentation
Alice -> Bob: secretBox1Alice, secretBox2Alice

principal Bob[
	ephemeralSecretBob = ephemeralAPub^ephemeralB
	longTermSecretBob = ephemeralAPub^longTermB
	masterSecret1Bob = HASH(ephemeralSecretBob, longTermSecretBob)
	sig1Bob = AEAD_DEC(masterSecret1Bob, secretBox1Alice, nil)?
	longTermAPub_Bob = AEAD_DEC(masterSecret1Bob, secretBox2Alice, nil)?
	sig1Valid = SIGNVERIF(longTermAPub_bob, HASH(longTermBPub, ephemeralSecretBob), sig1Bob)?
	longEphemeralSecretBob = longTermAPub_Bob^ephemeralB
]

// 4. Server Accept

principal Bob[
	sig2Bob = SIGN(longTermB, HASH(sig1Bob, longTermAPub_Bob, ephemeralSecretBob))
	masterSecret2Bob = HASH(ephemeralSecretBob, longTermSecretBob, longEphemeralSecretBob)
	secretBox1Bob = AEAD_ENC(masterSecret2Bob, sig2Bob, nil)
]

Bob -> Alice: secretBox1Bob

// 5. Send a message

principal Alice[
	knows private m1
	sig2Alice = AEAD_DEC(masterSecret2Alice, secretBox1Bob, nil)?
	sig2Valid = SIGNVERIF(longTermBPub, HASH(sig1Alice, longTermAPub, ephemeralSecretAlice), sig2Alice)?
	secretBoxM1Alice = AEAD_ENC(masterSecret2Alice, m1, nil)
]

Alice -> Bob: secretBoxM1Alice

principal Bob[
	knows private m2
	m1Bob = AEAD_DEC(masterSecret2Bob, secretBoxM1Alice, nil)?
	secretBoxM2Bob = AEAD_ENC(masterSecret2Bob, m2, nil)
]

Bob -> Alice: secretBoxM2Bob

principal Alice [
	m2Alice = AEAD_DEC(masterSecret2Alice, secretBoxM2Bob, nil)?
]

queries[
	confidentiality? m2
]
